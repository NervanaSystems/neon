# Kernel: sgemm_tn_128x32

# Copyright 2014 Nervana Systems Inc. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


<CONSTANT_MAPPING>
    addr_zero : 4x<128*16*2 + 32*16*2>
    szShareA  : 128*16
    szShareB  : 32*16

    gridDimA : c[0x0][0x14]
    gridDimB : c[0x0][0x18]

    param_C[0]        : c[0x0][0x140]
    param_C[1]        : c[0x0][0x144]
    param_A[0]        : c[0x0][0x148]
    param_A[1]        : c[0x0][0x14c]
    param_B[0]        : c[0x0][0x150]
    param_B[1]        : c[0x0][0x154]
    param_H[0]        : c[0x0][0x158]
    param_H[1]        : c[0x0][0x15c]
    param_lockAddr[0] : c[0x0][0x160]
    param_lockAddr[1] : c[0x0][0x164]
    param_alpha       : c[0x0][0x168]
    param_beta        : c[0x0][0x16c]
    param_xcutoff     : c[0x0][0x170]
    param_flags       : c[0x0][0x174]
    param_lda8        : c[0x0][0x178]
    param_ldb8        : c[0x0][0x17c]
    param_ldc         : c[0x0][0x180]
    param_ldh         : c[0x0][0x184]
    param_m           : c[0x0][0x188]
    param_n           : c[0x0][0x18c]
    param_k           : c[0x0][0x190]
    param_ldaz        : c[0x0][0x194]
    param_ldbz        : c[0x0][0x198]
    param_ldcz        : c[0x0][0x19c]
    param_loops       : c[0x0][0x1a0]
    param_dimB        : c[0x0][0x1a4]
    param_dimC        : c[0x0][0x1a8]
    param_dimH        : c[0x0][0x1ac]
    param_unrolling   : c[0x0][0x1b0]
    param_numBlks     : c[0x0][0x1b4]
    param_numAblks    : c[0x0][0x1b8]
</CONSTANT_MAPPING>

<REGISTER_MAPPING>

    32-79 ~ lda, lda4, ldb, ldaz, ldbz, ta<0-3>, tb, tid1, tidAX, tidBX, tidAY<1-3>, txa<1-3>, txb<1-3>, offsetB
    80-81 : baseB<0-1>

    0-31 : czero<00-31>

     3, 2,11,10 : cx<0-3>y0
     7, 6,15,14 : cx<0-3>y1
     1, 0, 9, 8 : cx<0-3>y2
     5, 4,13,12 : cx<0-3>y3
    19,18,27,26 : cx<0-3>y4
    23,22,31,30 : cx<0-3>y5
    17,16,25,24 : cx<0-3>y6
    21,20,29,28 : cx<0-3>y7

      32-43 : j0Ay<0-7>, j0Bx<0-3>
      44-55 : j1Ay<0-7>, j1Bx<0-3>
      56-67 : j2Ay<0-7>, j2Bx<0-3>
      68-79 : j3Ay<0-7>, j3Bx<0-3>

      80-83 : loadB<0-3>
      84-99 : load0A<0-3>, load1A<0-3>, load2A<0-3>, load3A<0-3>

    100-109 : trackB<0-1>, track0A<0-1>, track1A<0-1>, track2A<0-1>, track3A<0-1>

    110-120 ~ writeAs, writeBs, lda16, ldb16, k, tidAY, tidBY, txa, txb
    121-127 ~ swapBuf, readAs, readBs, tid, blkA, blkB, time_step

    32-39 : C00y<0-1>, C04y<0-1>, C08y<0-1>, C12y<0-1>
    40-47 : c<0-3>, d3, d2, d1, d0
    48-63 : H00y<0-1>, H04y<0-1>, H08y<0-1>, H12y<0-1>, h0, h1, h2, h3, baseC<0-1>, baseH<0-1>
    64-68 : blkId, nextBlk, lockAddr<0-1>, lockVal
   69-120 ~ tid31, tid96, ldc, ldcz, cx, ci, xmad_c, ldc1, ldc4, ldc60, writeCs, readCs, cy<00|04|08|12>, alpha, beta, flags, xcutoff, offsetC, offsetH, numBlk, predSave, ldh1, ldh4, ldh60

</REGISTER_MAPPING>

--:-:1:-:1      S2R tid,  SR_TID.X;
--:-:2:-:1      S2R blkA, SR_CTAID.Y;
--:-:3:-:1      S2R blkB, SR_CTAID.Z;

--:-:-:-:1      MOV time_step, RZ;
--:-:-:-:1      MOV flags, param_flags;

RNN_LOOP:

<SCHEDULE_BLOCK>
--:-:-:-:1      MOV k,    param_k;
--:-:-:-:1      MOV lda,  param_lda8;
--:-:-:-:1      MOV ldb,  param_ldb8;
--:-:-:-:1      SHR.U32 lda, lda, 5;
--:-:-:-:1      SHR.U32 ldb, ldb, 5;
--:-:-:-:1      MOV ldaz, param_ldaz;
--:-:-:-:1      MOV ldbz, param_ldbz;
--:-:-:-:1      SHL lda16, lda, 6;
--:-:-:-:1      SHL ldb16, ldb, 6;
--:-:-:-:1      SHL lda4,  lda, 2;

--:-:-:-:1      STS.128 [addr_zero], RZ;
<CODE>
    return join '', map sprintf("--:-:-:-:1      LDS.U.128 czero%02d, [addr_zero];\n", $_ * 4), 0..7;
</CODE>

--:-:-:-:6      LOP.AND.NZ   P0, RZ, flags, 4;
--:-:-:-:6  @P0 IADD offsetB, -time_step, param_unrolling;
--:-:-:-:6  @P0 IADD offsetB, offsetB,    -1;
--:-:-:-:6 @!P0 MOV  offsetB, time_step;

// baseB = param_B + dimB * time_step
--:-:-:-:1      XMAD     offsetB,   offsetB,   param_dimB, RZ;
--:-:-:-:1      LEA      baseB0.CC, offsetB,   param_B[0],     2;
--:-:-:-:1      LEA.HI.X baseB1,    offsetB,   param_B[1], RZ, 2;

// tidAX = (tid & 31) << 2
// tidAY = (tid >> 5)
01:-:-:-:1      LOP.AND tidAX, tid,   31;
--:-:-:-:1      SHL     tidAX, tidAX, 2;
--:-:-:-:1      SHR.U32 tidAY, tid,   5;

// tidBX = (tid & 7) << 2
// tidBY = (tid >> 3)
01:-:-:-:1      LOP.AND tidBX, tid,   7;
--:-:-:-:1      SHL     tidBX, tidBX, 2;
--:-:-:-:1      SHR.U32 tidBY, tid,   3;

// trackA += (blkA*128 + tidAX + lda*tidAY) * 4
02:-:-:-:1      ISCADD   txa, blkA, tidAX, 7;
--:-:-:-:1      XMAD.LO2 ta0, lda,  tidAY, txa;
08:-:-:-:1      XMAD.LO2 ta0, ldaz, RZ,    ta0;
--:-:-:-:1      IADD     ta1, ta0, lda4;
--:-:-:-:1      IADD     ta2, ta1, lda4;
--:-:-:-:1      IADD     ta3, ta2, lda4;

--:-:-:-:1      LEA      track0A0.CC, ta0, param_A[0],     2;
--:-:-:-:1      LEA.HI.X track0A1,    ta0, param_A[1], RZ, 2;
--:-:-:-:1      LEA      track1A0.CC, ta1, param_A[0],     2;
--:-:-:-:1      LEA.HI.X track1A1,    ta1, param_A[1], RZ, 2;
--:-:-:-:1      LEA      track2A0.CC, ta2, param_A[0],     2;
--:-:-:-:1      LEA.HI.X track2A1,    ta2, param_A[1], RZ, 2;
--:-:-:-:1      LEA      track3A0.CC, ta3, param_A[0],     2;
--:-:-:-:1      LEA.HI.X track3A1,    ta3, param_A[1], RZ, 2;

// trackB += (blkB*32 + ldb*tidBY + tidBX) * 4
04:-:-:-:1      ISCADD   txb, blkB, tidBX,  5;
--:-:-:-:1      XMAD.LO2 tb,  ldb,  tidBY, txb;
08:-:-:-:1      XMAD.LO2 tb,  ldbz, RZ,    tb;
--:-:-:-:1      LEA      trackB0.CC, tb, baseB0,     2;
--:-:-:-:1      LEA.HI.X trackB1,    tb, baseB1, RZ, 2;

// writeAs = (tidAY*128 + tidAX) * 4
--:-:-:-:1      ISCADD writeAs, tidAY, tidAX, 7;
--:-:-:-:1      ISCADD writeAs, writeAs, 4x<szShareA + szShareB>, 2;

// writeBs = (tidBY*32 + tidBX) * 4
--:-:-:-:1      ISCADD writeBs, tidBY, tidBX, 5;
--:-:-:-:1      ISCADD writeBs, writeBs, 4x<szShareA*2 + szShareB>, 2;

// readAs  = (((tid & 0x70) >> 3) | (tid & 1)) << 4;
--:-:-:-:1      LOP.AND tid1,   tid,    1;
--:-:-:-:1      LOP.AND readAs, tid,    0x70;
--:-:-:-:1      SHR.U32 readAs, readAs, 3;
--:-:-:-:1      LOP.OR  readAs, readAs, tid1;
--:-:-:-:1      SHL     readAs, readAs, 4;
// readBs = ((tid >> 1) & 7) << 4 + 4x<128*8>;
--:-:-:-:1      BFE.U32 readBs, tid,    0x301; // 3 bits at position 1
--:-:-:-:1      ISCADD  readBs, readBs, 4x<szShareA>, 4;

--:-:-:-:1      MOV32I swapBuf, -4x<szShareA + szShareB>;
</SCHEDULE_BLOCK>

REMAINDER:

<SCHEDULE_BLOCK>

--:-:-:-:1      IADD tidAY1, tidAY, 4;
--:-:-:-:1      IADD tidAY2, tidAY, 8;
--:-:-:-:1      IADD tidAY3, tidAY, 12;

<CODE>
    our $vec;
    return $vec ? q{
--:-:-:-:1      ISETP.LT.AND P5, PT, txa, param_m, PT;
--:-:-:-:1      ISETP.LT.AND P6, PT, txb, param_n, PT;

--:-:-:-:1      ISETP.LT.AND P0, PT, tidAY,  k, P5;
--:-:-:-:1      ISETP.LT.AND P1, PT, tidAY1, k, P5;
--:-:-:-:1      ISETP.LT.AND P2, PT, tidAY2, k, P5;
--:-:-:-:1      ISETP.LT.AND P3, PT, tidAY3, k, P5;
--:-:-:-:1      ISETP.LT.AND P4, PT, tidBY,  k, P6;

<ORDERED>
--:-:1:-:1  @P0 LDG.E.128 load0A, [track0A];
--:-:2:-:1  @P1 LDG.E.128 load1A, [track1A];
--:-:3:-:1  @P2 LDG.E.128 load2A, [track2A];
--:-:4:-:1  @P3 LDG.E.128 load3A, [track3A];
--:-:5:-:1  @P4 LDG.E.128 loadB,  [trackB];
</ORDERED>

<ORDERED>
--:-:6:-:1 @!P0 LDS.U.128 load0A, [addr_zero];
--:-:6:-:1 @!P1 LDS.U.128 load1A, [addr_zero];
--:-:6:-:1 @!P2 LDS.U.128 load2A, [addr_zero];
--:-:6:-:1 @!P3 LDS.U.128 load3A, [addr_zero];
--:-:6:-:2 @!P4 LDS.U.128 loadB,  [addr_zero];
</ORDERED>

    } : q{

--:-:-:-:1      IADD txa1,  txa,  1;
--:-:-:-:1      IADD txa2,  txa,  2;
--:-:-:-:1      IADD txa3,  txa,  3;

--:-:-:-:1      ISETP.LT.AND P4, PT, tidAY, k, PT;
--:-:-:-:1      ISETP.LT.AND P0, PT, txa,  param_m, P4;
--:-:-:-:1      ISETP.LT.AND P1, PT, txa1, param_m, P4;
--:-:-:-:1      ISETP.LT.AND P2, PT, txa2, param_m, P4;
--:-:-:-:1      ISETP.LT.AND P3, PT, txa3, param_m, P4;
<ORDERED>
--:-:1:-:1  @P0 LDG.E load0A0, [track0A + 4x<0>];
--:-:1:-:1  @P1 LDG.E load0A1, [track0A + 4x<1>];
--:-:1:-:1  @P2 LDG.E load0A2, [track0A + 4x<2>];
--:-:1:-:1  @P3 LDG.E load0A3, [track0A + 4x<3>];
</ORDERED>
--:-:-:-:1 @!P0 MOV load0A0, RZ;
--:-:-:-:1 @!P1 MOV load0A1, RZ;
--:-:-:-:1 @!P2 MOV load0A2, RZ;
--:-:-:-:1 @!P3 MOV load0A3, RZ;

--:-:-:-:1      ISETP.LT.AND P5, PT, tidAY1, k, PT;
--:-:-:-:1      ISETP.LT.AND P0, PT, txa,  param_m, P5;
--:-:-:-:1      ISETP.LT.AND P1, PT, txa1, param_m, P5;
--:-:-:-:1      ISETP.LT.AND P2, PT, txa2, param_m, P5;
--:-:-:-:1      ISETP.LT.AND P3, PT, txa3, param_m, P5;
<ORDERED>
--:-:2:-:1  @P0 LDG.E load1A0, [track1A + 4x<0>];
--:-:2:-:1  @P1 LDG.E load1A1, [track1A + 4x<1>];
--:-:2:-:1  @P2 LDG.E load1A2, [track1A + 4x<2>];
--:-:2:-:1  @P3 LDG.E load1A3, [track1A + 4x<3>];
</ORDERED>
--:-:-:-:1 @!P0 MOV load1A0, RZ;
--:-:-:-:1 @!P1 MOV load1A1, RZ;
--:-:-:-:1 @!P2 MOV load1A2, RZ;
--:-:-:-:1 @!P3 MOV load1A3, RZ;

--:-:-:-:1      ISETP.LT.AND P6, PT, tidAY2, k, PT;
--:-:-:-:1      ISETP.LT.AND P0, PT, txa,  param_m, P6;
--:-:-:-:1      ISETP.LT.AND P1, PT, txa1, param_m, P6;
--:-:-:-:1      ISETP.LT.AND P2, PT, txa2, param_m, P6;
--:-:-:-:1      ISETP.LT.AND P3, PT, txa3, param_m, P6;
<ORDERED>
--:-:3:-:1  @P0 LDG.E load2A0, [track2A + 4x<0>];
--:-:3:-:1  @P1 LDG.E load2A1, [track2A + 4x<1>];
--:-:3:-:1  @P2 LDG.E load2A2, [track2A + 4x<2>];
--:-:3:-:1  @P3 LDG.E load2A3, [track2A + 4x<3>];
</ORDERED>
--:-:-:-:1 @!P0 MOV load2A0, RZ;
--:-:-:-:1 @!P1 MOV load2A1, RZ;
--:-:-:-:1 @!P2 MOV load2A2, RZ;
--:-:-:-:1 @!P3 MOV load2A3, RZ;

--:-:-:-:1      ISETP.LT.AND P5, PT, tidAY3, k, PT;
--:-:-:-:1      ISETP.LT.AND P0, PT, txa,  param_m, P5;
--:-:-:-:1      ISETP.LT.AND P1, PT, txa1, param_m, P5;
--:-:-:-:1      ISETP.LT.AND P2, PT, txa2, param_m, P5;
--:-:-:-:1      ISETP.LT.AND P3, PT, txa3, param_m, P5;
<ORDERED>
--:-:4:-:1  @P0 LDG.E load3A0, [track3A + 4x<0>];
--:-:4:-:1  @P1 LDG.E load3A1, [track3A + 4x<1>];
--:-:4:-:1  @P2 LDG.E load3A2, [track3A + 4x<2>];
--:-:4:-:1  @P3 LDG.E load3A3, [track3A + 4x<3>];
</ORDERED>
--:-:-:-:1 @!P0 MOV load3A0, RZ;
--:-:-:-:1 @!P1 MOV load3A1, RZ;
--:-:-:-:1 @!P2 MOV load3A2, RZ;
--:-:-:-:1 @!P3 MOV load3A3, RZ;

--:-:-:-:1      IADD txb1,  txb,  1;
--:-:-:-:1      IADD txb2,  txb,  2;
--:-:-:-:1      IADD txb3,  txb,  3;

--:-:-:-:1      ISETP.LT.AND P4, PT, tidBY, k, PT;
--:-:-:-:1      ISETP.LT.AND P0, PT, txb,  param_n, P4;
--:-:-:-:1      ISETP.LT.AND P1, PT, txb1, param_n, P4;
--:-:-:-:1      ISETP.LT.AND P2, PT, txb2, param_n, P4;
--:-:-:-:1      ISETP.LT.AND P3, PT, txb3, param_n, P4;
<ORDERED>
--:-:5:-:1  @P0 LDG.E loadB0, [trackB + 4x<0>];
--:-:5:-:1  @P1 LDG.E loadB1, [trackB + 4x<1>];
--:-:5:-:1  @P2 LDG.E loadB2, [trackB + 4x<2>];
--:-:5:-:1  @P3 LDG.E loadB3, [trackB + 4x<3>];
</ORDERED>
--:-:-:-:1 @!P0 MOV loadB0, RZ;
--:-:-:-:1 @!P1 MOV loadB1, RZ;
--:-:-:-:1 @!P2 MOV loadB2, RZ;
--:-:-:-:1 @!P3 MOV loadB3, RZ;

--:-:-:-:1      ISETP.LT.AND P5, PT, txa, param_m, PT;
--:-:-:-:1      ISETP.LT.AND P6, PT, txb, param_n, PT;
    };
</CODE>

--:-:-:-:1      ISETP.GE.AND P2, PT, k, 32, P5;
--:-:-:-:1      ISETP.GE.AND P3, PT, k, 32, P5;
--:-:-:-:1      ISETP.GE.AND P5, PT, k, 32, P5;
--:-:-:-:1      ISETP.GE.AND P6, PT, k, 32, P6;

// bDoRemainder = k & 15 && k > 16
--:-:-:-:0      LOP.AND.NZ P1, RZ, k, 15;

</SCHEDULE_BLOCK>

21:-:-:-:1      STS.128 [writeAs + 4x<0*128>], load0A;
--:-:-:-:6      IADD   track0A0.CC, track0A0, lda16;
--:-:-:-:0      IADD.X track0A1,    track0A1, RZ;

02:-:-:-:1      STS.128 [writeAs + 4x<4*128>], load1A;
--:-:-:-:6      IADD   track1A0.CC, track1A0, lda16;
--:-:-:-:0      IADD.X track1A1,    track1A1, RZ;

04:-:-:-:1      STS.128 [writeAs + 4x<8*128>], load2A;
--:-:-:-:6      IADD   track2A0.CC, track2A0, lda16;
--:-:-:-:0      IADD.X track2A1,    track2A1, RZ;

08:-:-:-:1      STS.128 [writeAs + 4x<12*128>], load3A;
--:-:-:-:6      IADD   track3A0.CC, track3A0, lda16;
--:-:-:-:0      IADD.X track3A1,    track3A1, RZ;

10:-:-:-:1      STS.128 [writeBs], loadB;
--:-:-:-:1      IADD   trackB0.CC, trackB0, ldb16;

--:-:-:-:1      ISETP.GT.AND P1, PT, k, 16, P1;

--:-:-:-:1      IADD readBs,  readBs, -swapBuf;
--:-:-:-:0      IADD readAs,  readAs, -swapBuf;
--:-:-:-:5      BAR.SYNC 0;
--:-:-:-:1      IADD writeBs, writeBs, swapBuf;
--:-:-:-:1      IADD writeAs, writeAs, swapBuf;
--:-:-:-:1      IADD swapBuf, RZ, -swapBuf;

--:-:-:-:0      IADD.X trackB1,    trackB1, RZ;

<CODE>
    our $vec;
    return $vec ? q{
--:-:3:-:1  @P5 LDG.E.128 load0A, [track0A];
--:-:4:-:1  @P5 LDG.E.128 load1A, [track1A];
--:-:5:-:1  @P5 LDG.E.128 load2A, [track2A];
--:-:5:-:1  @P5 LDG.E.128 load3A, [track3A];
--:-:6:-:1  @P6 LDG.E.128 loadB,  [trackB];
    } : q{
--:-:3:-:1  @P5 LDG.E load0A0, [track0A + 4x<0>];
--:-:3:-:1  @P5 LDG.E load0A1, [track0A + 4x<1>];
--:-:3:-:1  @P5 LDG.E load0A2, [track0A + 4x<2>];
--:-:3:-:1  @P5 LDG.E load0A3, [track0A + 4x<3>];

--:-:4:-:1  @P5 LDG.E load1A0, [track1A + 4x<0>];
--:-:4:-:1  @P5 LDG.E load1A1, [track1A + 4x<1>];
--:-:4:-:1  @P5 LDG.E load1A2, [track1A + 4x<2>];
--:-:4:-:1  @P5 LDG.E load1A3, [track1A + 4x<3>];

--:-:5:-:1  @P5 LDG.E load2A0, [track2A + 4x<0>];
--:-:5:-:1  @P5 LDG.E load2A1, [track2A + 4x<1>];
--:-:5:-:1  @P5 LDG.E load2A2, [track2A + 4x<2>];
--:-:5:-:1  @P5 LDG.E load2A3, [track2A + 4x<3>];

--:-:5:-:1  @P5 LDG.E load3A0, [track3A + 4x<0>];
--:-:5:-:1  @P5 LDG.E load3A1, [track3A + 4x<1>];
--:-:5:-:1  @P5 LDG.E load3A2, [track3A + 4x<2>];
--:-:5:-:1  @P5 LDG.E load3A3, [track3A + 4x<3>];

--:-:6:-:1  @P6 LDG.E loadB0, [trackB + 4x<0>];
--:-:6:-:1  @P6 LDG.E loadB1, [trackB + 4x<1>];
--:-:6:-:1  @P6 LDG.E loadB2, [trackB + 4x<2>];
--:-:6:-:1  @P6 LDG.E loadB3, [trackB + 4x<3>];
    };
</CODE>

<CODE>
    our $vec;
    our $shiftAX = 0;
    our $shiftBX = 0;
    our %insert =
    (
        j0c6   => "--:-:-:-:1      IADD k, k, -16;\n",
        j0c14  => "--:-:-:-:1      ISETP.GE.AND P0, PT, k, 16, PT;\n",

        j3c6   => "04:3:-:-:1  \@P0 STS.128 [writeAs + 4x< 0*128>], load0A;\n",
        j5c6   => "08:4:-:-:1  \@P0 STS.128 [writeAs + 4x< 4*128>], load1A;\n",
        j7c6   => "10:-:-:-:1  \@P0 STS.128 [writeAs + 4x< 8*128>], load2A;\n",
        j9c6   => "--:5:-:-:1  \@P0 STS.128 [writeAs + 4x<12*128>], load3A;\n",
        j11c6  => "20:6:-:-:1  \@P0 STS.128 [writeBs], loadB;\n",

        j3c7   => "--:-:-:-:1  \@P2 IADD   track0A0.CC, track0A0, lda16;\n",
        j3c13  => "--:-:-:-:1  \@P2 IADD.X track0A1,    track0A1, RZ;\n",
        j5c7   => "--:-:-:-:1  \@P3 IADD   track1A0.CC, track1A0, lda16;\n",
        j5c13  => "--:-:-:-:1  \@P3 IADD.X track1A1,    track1A1, RZ;\n",
        j7c7   => "--:-:-:-:1  \@P5 IADD   track2A0.CC, track2A0, lda16;\n",
        j7c13  => "--:-:-:-:1  \@P5 IADD.X track2A1,    track2A1, RZ;\n",
        j9c7   => "--:-:-:-:1  \@P5 IADD   track3A0.CC, track3A0, lda16;\n",
        j9c13  => "--:-:-:-:1  \@P5 IADD.X track3A1,    track3A1, RZ;\n",
        j11c7  => "--:-:-:-:1  \@P6 IADD   trackB0.CC,  trackB0,  ldb16;\n",
        j11c13 => "--:-:-:-:1  \@P6 IADD.X trackB1,     trackB1,  RZ;\n",

        j3c14  => "--:-:-:-:1      ISETP.GE.AND P2, PT, k, 32, P2;\n",
        j5c14  => "--:-:-:-:1      ISETP.GE.AND P3, PT, k, 32, P3;\n",
        j9c14  => "--:-:-:-:1      ISETP.GE.AND P5, PT, k, 32, P5;\n",
        j11c14 => "--:-:-:-:1      ISETP.GE.AND P6, PT, k, 32, P6;\n",

        j13c31 => "--:-:-:-:5  \@P0 BAR.SYNC 0;\n" .
                  "--:-:-:-:1  \@P0 IADD readAs,  readAs, -swapBuf;\n" .
                  "--:-:-:-:1  \@P0 IADD readBs,  readBs, -swapBuf;\n" .
                  "--:-:-:-:1  \@P0 IADD writeAs, writeAs, swapBuf;\n" .
                  "--:-:-:-:1  \@P0 IADD writeBs, writeBs, swapBuf;\n" .
                  "--:-:-:-:1  \@P0 IADD swapBuf, RZ,     -swapBuf;\n",

        ($vec ?
            (
                j3c29  => "04:-:3:-:1  \@P2 LDG.E.128 load0A, [track0A];\n",
                j5c29  => "08:-:4:-:1  \@P3 LDG.E.128 load1A, [track1A];\n",
                j9c29  => "10:-:-:-:1  \@P5 LDG.E.128 load2A, [track2A];\n",
                j9c31  => "--:-:5:-:1  \@P5 LDG.E.128 load3A, [track3A];\n",
                j11c29 => "20:-:6:-:1  \@P6 LDG.E.128 loadB,  [trackB];\n",
            ) :
            (
                j3c29  => "04:-:-:-:1  \@P2 LDG.E load0A0, [track0A + 4x<0>];\n",
                j3c31  => "--:-:-:-:1  \@P2 LDG.E load0A1, [track0A + 4x<1>];\n",
                j4c1   => "--:-:-:-:1  \@P2 LDG.E load0A2, [track0A + 4x<2>];\n",
                j4c3   => "--:-:3:-:1  \@P2 LDG.E load0A3, [track0A + 4x<3>];\n",

                j5c29  => "08:-:-:-:1  \@P3 LDG.E load1A0, [track1A + 4x<0>];\n",
                j5c31  => "--:-:-:-:1  \@P3 LDG.E load1A1, [track1A + 4x<1>];\n",
                j6c1   => "--:-:-:-:1  \@P3 LDG.E load1A2, [track1A + 4x<2>];\n",
                j6c3   => "--:-:4:-:1  \@P3 LDG.E load1A3, [track1A + 4x<3>];\n",

                j9c29  => "10:-:-:-:1  \@P5 LDG.E load2A0, [track2A + 4x<0>];\n",
                j9c31  => "--:-:-:-:1  \@P5 LDG.E load2A1, [track2A + 4x<1>];\n",
                j10c1  => "--:-:-:-:1  \@P5 LDG.E load2A2, [track2A + 4x<2>];\n",
                j10c3  => "--:-:-:-:1  \@P5 LDG.E load2A3, [track2A + 4x<3>];\n",

                j10c8  => "--:-:-:-:1  \@P5 LDG.E load3A0, [track3A + 4x<0>];\n",
                j10c10 => "--:-:-:-:1  \@P5 LDG.E load3A1, [track3A + 4x<1>];\n",
                j10c12 => "--:-:-:-:1  \@P5 LDG.E load3A2, [track3A + 4x<2>];\n",
                j10c14 => "--:-:5:-:1  \@P5 LDG.E load3A3, [track3A + 4x<3>];\n",

                j11c29 => "20:-:-:-:1  \@P6 LDG.E loadB0, [trackB + 4x<0>];\n",
                j11c31 => "--:-:-:-:1  \@P6 LDG.E loadB1, [trackB + 4x<1>];\n",
                j12c1  => "--:-:-:-:1  \@P6 LDG.E loadB2, [trackB + 4x<2>];\n",
                j12c3  => "--:-:6:-:1  \@P6 LDG.E loadB3, [trackB + 4x<3>];\n",
            )
        ),

        j15c31 => "--:-:-:Y:5  \@P0 BRA.U LOOP;\n" .
                  "--:-:-:Y:5  \@P1 BRA.U REMAINDER;\n"
    );
    return '';
</CODE>

<INCLUDE file="sgemm_rnn_bprop_common_128x32.sass"/>
