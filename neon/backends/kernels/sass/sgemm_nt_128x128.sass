# Kernel: sgemm_nt_128x128

# Copyright 2014 Nervana Systems Inc. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


<CONSTANT_MAPPING>
    addr_zero  : 4x<128*8*4>

    gridDimA : c[0x0][0x14]
    gridDimB : c[0x0][0x18]

    param_C[0]      : c[0x0][0x140]
    param_C[1]      : c[0x0][0x144]
    param_A[0]      : c[0x0][0x148]
    param_A[1]      : c[0x0][0x14c]
    param_B[0]      : c[0x0][0x150]
    param_B[1]      : c[0x0][0x154]
    param_alpha     : c[0x0][0x158]
    param_beta      : c[0x0][0x15c]
    param_flags     : c[0x0][0x160]
    param_lda       : c[0x0][0x164]
    param_ldb       : c[0x0][0x168]
    param_ldc       : c[0x0][0x16c]
    param_m         : c[0x0][0x170]
    param_n         : c[0x0][0x174]
    param_k         : c[0x0][0x178]
    param_ldaz      : c[0x0][0x17c]
    param_ldbz      : c[0x0][0x180]
    param_ldcz      : c[0x0][0x184]
    param_loops     : c[0x0][0x188]
</CONSTANT_MAPPING>

<REGISTER_MAPPING>

    64-95   ~ blkA, blkB, blkZ, tidX, blk, lda, ldb, ldaz, ldbz, tid1, tid2, tid7, tid127, txa, txb, xmad_ta, xmad_tb, tid128

    0-63    : czero<00-63>

     3, 2,11,10,19,18,27,26 : cx<0-7>y0
     7, 6,15,14,23,22,31,30 : cx<0-7>y1
     1, 0, 9, 8,17,16,25,24 : cx<0-7>y2
     5, 4,13,12,21,20,29,28 : cx<0-7>y3
    35,34,43,42,51,50,59,58 : cx<0-7>y4
    39,38,47,46,55,54,63,62 : cx<0-7>y5
    33,32,41,40,49,48,57,56 : cx<0-7>y6
    37,36,45,44,53,52,61,60 : cx<0-7>y7

    64-67   ~ k1, k2, k3

    64-79   : j0Ay<0-7>, j0Bx<0-7>
    80-95   : j1Ay<0-7>, j1Bx<0-7>

    96-111  : loadA<0-7>,  loadB<0-7>
    112-115 : trackA<0-1>, trackB<0-1>

    116-121 ~ writeS, k, tidY, ta, tb, loop
    122-127 ~ readAs, readBs, tid

    64-75   ~ ldc, ci, xmad_c, tid_31, tid_96, tid_128, blockA, blockB, blockZ
    64-75   : c<0-7>, d3, d2, d1, d0
    76-85   : C00y<0-1>, C04y<0-1>, C08y<0-1>, C12y<0-1>
    86-121  ~ ldc1, ldc4, ldc60, ldcz, writeCs, readCs, cx<00|64>, cy<00|04|08|12>, alpha, beta, flags

</REGISTER_MAPPING>

--:-:1:-:1      S2R tid,  SR_TID.X;
--:-:2:-:1      S2R blkA, SR_CTAID.Y;
--:-:3:-:1      S2R blkB, SR_CTAID.Z;
--:-:4:-:1      S2R blkZ, SR_CTAID.X;

<SCHEDULE_BLOCK>
--:-:-:-:1      MOV k,  param_k;
--:-:-:-:1      LOP.AND tid1, tid,  1;

--:-:-:-:1      STS.128 [addr_zero], RZ;
<CODE>
        join('', map sprintf("--:-:-:-:1      LDS.U.128 czero%02d, [addr_zero];\n", $_ * 4), 0..15);
</CODE>
--:-:-:-:1      MOV loop, RZ;
--:-:-:-:1      MOV lda, param_lda;
--:-:-:-:1      MOV ldb, param_ldb;
--:-:-:-:1      MOV ldaz, param_ldaz;
--:-:-:-:1      MOV ldbz, param_ldbz;

// tidY  = tid1 << 2
--:-:-:-:1      SHL tidY, tid1, 2;

// tidX = tid >> 1
01:-:-:-:1      SHR.U32 tidX, tid, 1;

// trackA += 4 * ((blkA*128 + tidX) * lda + tidY)
02:-:-:-:1      ISCADD   txa, blkA, tidX, 7;
--:-:-:-:1      XMAD.LO  ta,  lda,  txa,  tidY, xmad_ta;
08:-:-:-:1      XMAD.LO2 ta,  ldaz, blkZ, ta;
--:-:-:-:1      LEA      trackA0.CC, ta, param_A[0], 0x2;
--:-:-:-:1      LEA.HI.X trackA1,    ta, param_A[1], RZ, 0x2;

// trackB += 4 * ((blkB*128 + tidX) * ldb + tidY)
04:-:-:-:1      ISCADD   txb, blkB, tidX, 7;
--:-:-:-:1      XMAD.LO  tb,  ldb,  txb,  tidY, xmad_tb;
08:-:-:-:1      XMAD.LO2 tb,  ldbz, blkZ, tb;
--:-:-:-:1      LEA      trackB0.CC, tb, param_B[0], 0x2;
--:-:-:-:1      LEA.HI.X trackB1,    tb, param_B[1], RZ, 0x2;

--:-:-:-:1      ISETP.LT.AND P5, PT, txa, param_m, PT;
--:-:-:-:1      ISETP.LT.AND P6, PT, txb, param_n, PT;

// writeS = 4 * (128 * tidY + tidX)
--:-:-:-:1      ISCADD  writeS, tidY, tidX, 7;
--:-:-:-:1      SHL     writeS, writeS, 2;

--:-:-:-:1      LOP.XOR writeS, writeS, 4x<128*8*2>;

// readAs  = (((tid & 0x70) >> 3) | (tid & 1)) << 4
--:-:-:-:1      LOP.AND readAs, tid,    0x70;
--:-:-:-:1      SHR.U32 readAs, readAs, 3;
--:-:-:-:1      LOP.OR  readAs, readAs, tid1;
--:-:-:-:1      SHL     readAs, readAs, 4;

// readBs = ((tid128 >> 4) | ((tid >> 1) & 7)) << 4 + 4096;
--:-:-:-:1      LOP.AND tid128, tid,  128;
--:-:-:-:1      BFE.U32 tid7,   tid,    0x301; // 3 bits at position 1
--:-:-:-:1      SHR.U32 readBs, tid128, 4;
--:-:-:-:1      LOP.OR  readBs, readBs, tid7;
--:-:-:-:1      ISCADD  readBs, readBs, 4x<128*8>, 4;
</SCHEDULE_BLOCK>

REMAINDER:

<CODE>
    our $vec;
    return $vec ? q{

// k must be multiple of 8
--:-:2:-:1  @P5 LDG.E.CI.128 loadA0, [trackA + 4x<0>];
--:-:2:-:1  @P5 LDG.E.CI.128 loadA4, [trackA + 4x<8>];

--:-:3:-:1  @P6 LDG.E.CI.128 loadB0, [trackB + 4x<0>];
--:5:4:-:1  @P6 LDG.E.CI.128 loadB4, [trackB + 4x<8>];

--:-:-:-:1 @!P5 LDS.U.128 loadA0, [addr_zero];
--:-:6:-:1 @!P6 LDS.U.128 loadB0, [addr_zero];
--:-:-:-:1 @!P5 LDS.U.128 loadA4, [addr_zero];
--:-:-:-:1 @!P6 LDS.U.128 loadB4, [addr_zero];

--:-:-:-:0      PSETP.AND.AND P1, PT, PT, PT, PT;

22:-:-:-:1      STS [writeS + 4x<0*128>], loadA0;
--:-:-:-:1      STS [writeS + 4x<1*128>], loadA1;
--:-:-:-:1      STS [writeS + 4x<2*128>], loadA2;
--:-:-:-:1      STS [writeS + 4x<3*128>], loadA3;

--:-:-:-:6      IADD   trackA0.CC, trackA0, 4x<16>;
--:-:-:-:0      IADD.X trackA1, trackA1, RZ;

24:-:-:-:1      STS [writeS + 4x< 8*128>], loadB0;
--:-:-:-:1      STS [writeS + 4x< 9*128>], loadB1;
--:-:-:-:1      STS [writeS + 4x<10*128>], loadB2;
--:-:-:-:1      STS [writeS + 4x<11*128>], loadB3;

10:-:-:-:6      IADD   trackB0.CC, trackB0, 4x<16>;
--:-:-:-:1      IADD.X trackB1, trackB1, RZ;

--:-:-:-:1      LOP.XOR readAs, readAs, 4x<128*8*2>;
--:-:-:-:0      LOP.XOR readBs, readBs, 4x<128*8*2>;
01:-:-:-:5      BAR.SYNC 0;
--:-:-:-:0      LOP.XOR writeS, writeS, 4x<128*8*2>;

    } : q{

<SCHEDULE_BLOCK>
--:-:-:-:1      IADD k1, tidY, 1;
--:-:-:-:1      IADD k2, tidY, 2;
--:-:-:-:1      IADD k3, tidY, 3;

--:-:-:-:1      ISETP.LT.AND P0, PT, tidY, k, P5;
--:-:-:-:1      ISETP.LT.AND P1, PT, k1, k, P5;
--:-:-:-:1      ISETP.LT.AND P2, PT, k2, k, P5;
--:-:-:-:1      ISETP.LT.AND P3, PT, k3, k, P5;

--:-:2:-:1  @P0 LDG.E.CI loadA0, [trackA + 4x<0>];
--:-:2:-:1  @P1 LDG.E.CI loadA1, [trackA + 4x<1>];
--:-:2:-:1  @P2 LDG.E.CI loadA2, [trackA + 4x<2>];
--:-:2:-:1  @P3 LDG.E.CI loadA3, [trackA + 4x<3>];

--:-:-:-:1 @!P0 MOV loadA0, RZ;
--:-:-:-:1 @!P1 MOV loadA1, RZ;
--:-:-:-:1 @!P2 MOV loadA2, RZ;
--:-:-:-:1 @!P3 MOV loadA3, RZ;

--:-:-:-:1      ISETP.LT.AND P0, PT, tidY, k, P6;
--:-:-:-:1      ISETP.LT.AND P1, PT, k1, k, P6;
--:-:-:-:1      ISETP.LT.AND P2, PT, k2, k, P6;
--:-:-:-:1      ISETP.LT.AND P3, PT, k3, k, P6;

--:-:3:-:1  @P0 LDG.E.CI loadB0, [trackB + 4x<0>];
--:-:3:-:1  @P1 LDG.E.CI loadB1, [trackB + 4x<1>];
--:-:3:-:1  @P2 LDG.E.CI loadB2, [trackB + 4x<2>];
--:-:3:-:1  @P3 LDG.E.CI loadB3, [trackB + 4x<3>];

--:-:-:-:1 @!P0 MOV loadB0, RZ;
--:-:-:-:1 @!P1 MOV loadB1, RZ;
--:-:-:-:1 @!P2 MOV loadB2, RZ;
--:-:-:-:1 @!P3 MOV loadB3, RZ;
</SCHEDULE_BLOCK>

// bDoRemainder = k & 7 && k > 8
--:-:-:-:0      LOP.AND.NZ P1, RZ, k, 7;

02:-:-:-:1      STS [writeS + 4x<0*128>], loadA0;
--:-:-:-:1      STS [writeS + 4x<1*128>], loadA1;
--:-:-:-:1      STS [writeS + 4x<2*128>], loadA2;
--:-:-:-:1      STS [writeS + 4x<3*128>], loadA3;

--:-:-:-:6      IADD   trackA0.CC, trackA0, 4x<8>;
--:-:-:-:0      IADD.X trackA1, trackA1, RZ;

04:-:-:-:1      STS [writeS + 4x< 8*128>], loadB0;
--:-:-:-:1      STS [writeS + 4x< 9*128>], loadB1;
--:-:-:-:1      STS [writeS + 4x<10*128>], loadB2;
--:-:-:-:1      STS [writeS + 4x<11*128>], loadB3;

--:-:-:-:6      IADD   trackB0.CC, trackB0, 4x<8>;
--:-:-:-:1      IADD.X trackB1, trackB1, RZ;

--:-:-:-:1      LOP.XOR readAs, readAs, 4x<128*8*2>;
--:-:-:-:0      LOP.XOR readBs, readBs, 4x<128*8*2>;
01:-:-:-:5      BAR.SYNC 0;
--:-:-:-:1      LOP.XOR writeS, writeS, 4x<128*8*2>;

--:-:-:-:0      ISETP.GT.AND P1, PT, k, 8, P1;
    };
</CODE>

<CODE>
    our $vec;
    our $vec;
    our @top = $vec ?
        ("--:-:-:-:1      ISETP.GE.AND P0, PT, k, 16, PT;\n") :
        ("--:-:-:-:1      ISETP.GE.AND P2, PT, k, 16, P5;\n");
    our %insert =
    (
        ($vec ?
            (
        j0c1  => "--:-:-:-:1      PSETP.AND.AND P1, PT, !P1, PT, PT;\n",
        j0c13 => "--:-:-:-:1      PSETP.AND.AND P2, PT, P0, P1, P5;\n",
        j0c14 => "--:-:-:-:1      PSETP.AND.AND P3, PT, P0, P1, P6;\n",

        j0c27 => "--:-:2:-:1  \@P2 LDG.E.CI.128 loadA0, [trackA + 4x<0>];\n",
        j0c29 => "--:-:2:-:1  \@P2 LDG.E.CI.128 loadA4, [trackA + 4x<8>];\n",

        j0c31 => "--:-:3:-:1  \@P3 LDG.E.CI.128 loadB0, [trackB + 4x<0>];\n",
        j0c33 => "08:5:4:-:1  \@P3 LDG.E.CI.128 loadB4, [trackB + 4x<8>];\n",

        j3c29 => "--:-:-:-:1 \@!P1 STS [writeS + 4x<0*128>], loadA4;\n",
        j3c31 => "--:-:-:-:1 \@!P1 STS [writeS + 4x<1*128>], loadA5;\n",
        j3c33 => "--:-:-:-:1 \@!P1 STS [writeS + 4x<2*128>], loadA6;\n",
        j3c35 => "--:-:-:-:1 \@!P1 STS [writeS + 4x<3*128>], loadA7;\n",

        j4c29 => "--:-:-:-:1 \@!P1 STS [writeS + 4x< 8*128>], loadB4;\n",
        j4c31 => "--:-:-:-:1 \@!P1 STS [writeS + 4x< 9*128>], loadB5;\n",
        j4c33 => "--:-:-:-:1 \@!P1 STS [writeS + 4x<10*128>], loadB6;\n",
        j4c35 => "--:-:-:-:1 \@!P1 STS [writeS + 4x<11*128>], loadB7;\n",

        j5c29 => "02:-:-:-:1  \@P1 STS [writeS + 4x<0*128>], loadA0;\n",
        j5c31 => "--:-:-:-:1  \@P1 STS [writeS + 4x<1*128>], loadA1;\n",
        j5c33 => "--:-:-:-:1  \@P1 STS [writeS + 4x<2*128>], loadA2;\n",
        j5c35 => "--:-:-:-:1  \@P1 STS [writeS + 4x<3*128>], loadA3;\n",

        j6c29 => "04:-:-:-:1  \@P1 STS [writeS + 4x< 8*128>], loadB0;\n",
        j6c31 => "--:-:-:-:1  \@P1 STS [writeS + 4x< 9*128>], loadB1;\n",
        j6c33 => "--:-:-:-:1  \@P1 STS [writeS + 4x<10*128>], loadB2;\n",
        j6c35 => "--:2:-:-:1  \@P1 STS [writeS + 4x<11*128>], loadB3;\n",

        j5c46 => "--:-:-:-:1  \@P2 IADD   trackA0.CC, trackA0, 4x<16>;\n",
        j5c54 => "--:-:-:-:1  \@P2 IADD.X trackA1,    trackA1, RZ;\n",

        j6c46 => "10:-:-:-:1  \@P3 IADD   trackB0.CC, trackB0, 4x<16>;\n",
        j6c54 => "--:-:-:-:1  \@P3 IADD.X trackB1,    trackB1, RZ;\n",

        j7c63 => "--:-:-:Y:5  \@P0 BRA.U LOOP;\n",
            ) :
            (
        j0c1  => "--:-:-:-:1      ISETP.GE.AND P3, PT, k, 16, P6;\n",
        j0c3  => "--:-:-:-:1      ISETP.GE.AND P0, PT, k, 16, PT;\n",

        j0c10 => "--:-:2:-:1  \@P2 LDG.E.CI loadA0, [trackA + 4x<0>];\n",
        j0c29 => "--:-:2:-:1  \@P2 LDG.E.CI loadA1, [trackA + 4x<1>];\n",
        j0c31 => "--:-:2:-:1  \@P2 LDG.E.CI loadA2, [trackA + 4x<2>];\n",
        j0c33 => "--:-:2:-:1  \@P2 LDG.E.CI loadA3, [trackA + 4x<3>];\n",

        j0c35 => "--:-:3:-:1  \@P3 LDG.E.CI loadB0, [trackB + 4x<0>];\n",
        j1c27 => "--:-:3:-:1  \@P3 LDG.E.CI loadB1, [trackB + 4x<1>];\n",
        j1c29 => "--:-:3:-:1  \@P3 LDG.E.CI loadB2, [trackB + 4x<2>];\n",
        j1c31 => "--:-:3:-:1  \@P3 LDG.E.CI loadB3, [trackB + 4x<3>];\n",

        j5c29 => "02:-:-:-:1  \@P0 STS [writeS + 4x<0*128>], loadA0;\n",
        j5c31 => "--:-:-:-:1  \@P0 STS [writeS + 4x<1*128>], loadA1;\n",
        j5c33 => "--:-:-:-:1  \@P0 STS [writeS + 4x<2*128>], loadA2;\n",
        j5c35 => "--:-:-:-:1  \@P0 STS [writeS + 4x<3*128>], loadA3;\n",

        j6c29 => "04:-:-:-:1  \@P0 STS [writeS + 4x< 8*128>], loadB0;\n",
        j6c31 => "--:-:-:-:1  \@P0 STS [writeS + 4x< 9*128>], loadB1;\n",
        j6c33 => "--:-:-:-:1  \@P0 STS [writeS + 4x<10*128>], loadB2;\n",
        j6c35 => "--:2:-:-:1  \@P0 STS [writeS + 4x<11*128>], loadB3;\n",

        j5c46 => "--:-:-:-:1  \@P2 IADD   trackA0.CC, trackA0, 4x<8>;\n",
        j5c54 => "--:-:-:-:1  \@P2 IADD.X trackA1,    trackA1, RZ;\n",

        j6c46 => "--:-:-:-:1  \@P3 IADD   trackB0.CC, trackB0, 4x<8>;\n",
        j6c54 => "--:-:-:-:1  \@P3 IADD.X trackB1,    trackB1, RZ;\n",

        j7c63 => "--:-:-:Y:5  \@P0 BRA.U LOOP;\n" .
                 "--:-:-:Y:5  \@P1 BRA.U REMAINDER;\n",
            ),
        ),

        j6c63 => "02:-:-:-:5  \@P0 BAR.SYNC 0;\n" .
                 "--:-:-:-:1  \@P0 LOP.XOR readAs, readAs, 4x<128*8*2>;\n" .
                 "--:-:-:-:1  \@P0 LOP.XOR readBs, readBs, 4x<128*8*2>;\n" .
                 "--:-:-:-:1  \@P0 LOP.XOR writeS, writeS, 4x<128*8*2>;\n" .
                 "--:-:-:-:1      IADD32I k, k, -8;\n",
    );
    return;
</CODE>

<INCLUDE file="sgemm_common_128x128.sass"/>
